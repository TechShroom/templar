plugins {
    id "com.techshroom.incise-blue" version "0.0.8" apply false
 	id "net.researchgate.release" version "2.6.0"
}

subprojects {
    apply plugin: 'com.techshroom.incise-blue'
    apply plugin: 'java-library'

    version = rootProject.version
    group = rootProject.group
    ext.url = rootProject.url
    ext.organization = rootProject.organization

    inciseBlue {
        plugins {
            apt
            license
            maven
            util
        }

        util {
            javaVersion = '1.8'
        }
    }

    configurations.all {
       resolutionStrategy {
         // fail eagerly on version conflict (includes transitive dependencies)
         // e.g. multiple different versions of the same dependency (group and name are equal)
         failOnVersionConflict()

         // force certain versions of dependencies (including transitive)
         force 'com.google.guava:guava:' + guavaVersion, 'com.google.code.findbugs:jsr305:3.0.1'

         cacheDynamicVersionsFor 10, 'minutes'
         // don't cache changing modules at all
         cacheChangingModulesFor 0, 'seconds'
       }
    }

    dependencies {
        api group: 'io.netty', name: 'netty-all', version: '4.1.22.Final'
        api group: 'com.techshroom', name: 'lettar', version: '0.3.1'
        api group: 'com.google.guava', name: 'guava', version: project.guavaVersion

        api group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'
        implementation group: 'com.techshroom', name: 'greenish-jungle', version: '0.0.3'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

        aptCompileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc4'
        aptCompileOnly group: 'com.google.auto.value', name: 'auto-value', version: '1.5.4'

        testImplementation group: 'junit', name: 'junit', version: '4.12'
    }
}

task build {}
build.dependsOn(subprojects.collect { it.build })
afterReleaseBuild.dependsOn(subprojects.collect { it.uploadArchives })
